#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "ИзменениеСодержимогоСтандарта" Тогда
		Прочитать();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СодержаниеСтандартаHTMLНаФормеПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НЕ (ДанныеСобытия.Свойство("href") И ЗначениеЗаполнено(ДанныеСобытия.href)) Тогда
		Возврат;
	КонецЕсли;
	
	СсылкаНаСтандартИТС = РаботаСHTMLКлиентСервер.СсылкаНаИТС(ДанныеСобытия.Href);
	
	ЗапуститьПриложениеАсинх(СсылкаНаСтандартИТС);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РедактироватьТекстСтандарта(Команда)
	
	Если Параметры.Ключ.Пустая() Или Модифицированность Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Сначала нужно сохранить стандарт'");
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура("СтандартРазработки", Объект.Ссылка);
	ОткрытьФорму("ОбщаяФорма.РедакторHTML", ПараметрыОткрытия, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти