
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбновитьДеревоСтандартов();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСтандартовВыбранПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ДеревоСтандартов.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = ДеревоСтандартов.НайтиПоИдентификатору(ТекущаяСтрока);
	
	Выбран = ТекущиеДанные.Выбран;
	Если Выбран Тогда
		УстановитьВыбранУРодительскойСтроки(ТекущиеДанные);
	КонецЕсли;
	УстановитьВыбранУПодчиненныхСтрок(ТекущиеДанные.ПолучитьЭлементы(), Выбран);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СобратьPDF(Команда)
	
	АдресВоВХ = СобратьPDFНаСервере();
	
	Если АдресВоВХ <> Неопределено Тогда
		ИмяФайлаPDF = "Система стандартов и методик разработки конфигураций 1С.pdf";
		НачатьПолучениеФайлаССервера(АдресВоВХ, ИмяФайлаPDF);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьДеревоСтандартов()
	
	Дерево = Справочники.СтандартыРазработки.ДеревоСтандартов();
	Дерево.Колонки.Добавить("Выбран", Новый ОписаниеТипов("Булево"));
	ЗначениеВДанныеФормы(Дерево, ДеревоСтандартов);
	
	УстановитьВыбранУПодчиненныхСтрок(ДеревоСтандартов.ПолучитьЭлементы(), Истина);
	
КонецПроцедуры

&НаСервере
Функция СобратьPDFНаСервере()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ДеревоСтрандартов = РеквизитФормыВЗначение("ДеревоСтандартов");
	ДокументHTML = ОбработкаОбъект.СобратьHTML(ДеревоСтрандартов);
	
	Возврат РаботаСPDF.КонвертироватьHTMLвPDF(ДокументHTML, "АдресВХ");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВыбранУПодчиненныхСтрок(СтрокиДерева, Выбран)
	
	Для Каждого СтрокаДерева Из СтрокиДерева Цикл
		СтрокаДерева.Выбран = Выбран И Не СтрокаДерева.НеИспользуется;
		УстановитьВыбранУПодчиненныхСтрок(СтрокаДерева.ПолучитьЭлементы(), Выбран);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВыбранУРодительскойСтроки(СтрокаДерева)
	
	РодительскаяСтрока = СтрокаДерева.ПолучитьРодителя();
	Если РодительскаяСтрока <> Неопределено И Не РодительскаяСтрока.Выбран Тогда
		РодительскаяСтрока.Выбран = Истина;
		УстановитьВыбранУРодительскойСтроки(РодительскаяСтрока);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
