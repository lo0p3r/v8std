
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьРазмерШрифтаПоУмолчанию();
	
	ОбновитьДеревоСтандартов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_СтандартыРазработки" Тогда
		ОбновитьДеревоСтандартов();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РазмерШрифтаРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	// Чтобы шаг был 2, но работали ограничения на минимальное и максимальное значения
	РазмерШрифта = РазмерШрифта + Направление;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоСтандартов

&НаКлиенте
Процедура ДеревоСтандартовВыбранПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ДеревоСтандартов.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = ДеревоСтандартов.НайтиПоИдентификатору(ТекущаяСтрока);
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Выбран = ТекущиеДанные.Выбран;
	Если Выбран Тогда
		УстановитьВыбранУРодительскойСтроки(ТекущиеДанные);
	КонецЕсли;
	УстановитьВыбранУПодчиненныхСтрок(ТекущиеДанные.ПолучитьЭлементы(), Выбран);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СобратьPDF(Команда)
	
	АдресВоВХ = СобратьPDFНаСервере();
	
	Если АдресВоВХ <> Неопределено Тогда
		ИмяФайлаPDF = "Система стандартов и методик разработки конфигураций 1С.pdf";
		НачатьПолучениеФайлаССервера(АдресВоВХ, ИмяФайлаPDF);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьДеревоСтандартов()
	
	Дерево = Справочники.СтандартыРазработки.ДеревоСтандартов();
	
	ЗначениеВДанныеФормы(Дерево, ДеревоСтандартов);
	
КонецПроцедуры

&НаСервере
Функция СобратьPDFНаСервере()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	Дерево = РеквизитФормыВЗначение("ДеревоСтандартов");
	ДокументHTML = ОбработкаОбъект.СобратьHTML(Дерево, РазмерШрифта);
	
	Возврат РаботаСPDF.КонвертироватьHTMLвPDF(ДокументHTML, "АдресВХ");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВыбранУПодчиненныхСтрок(СтрокиДерева, Выбран)
	
	Для Каждого СтрокаДерева Из СтрокиДерева Цикл
		СтрокаДерева.Выбран = Выбран И Не СтрокаДерева.НеИспользуется;
		УстановитьВыбранУПодчиненныхСтрок(СтрокаДерева.ПолучитьЭлементы(), Выбран);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВыбранУРодительскойСтроки(СтрокаДерева)
	
	РодительскаяСтрока = СтрокаДерева.ПолучитьРодителя();
	Если РодительскаяСтрока <> Неопределено И Не РодительскаяСтрока.Выбран Тогда
		РодительскаяСтрока.Выбран = Истина;
		УстановитьВыбранУРодительскойСтроки(РодительскаяСтрока);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьРазмерШрифтаПоУмолчанию()
	
	РазмерШрифта = РаботаСHTMLВызовСервера.РазмерШрифтаПоУмолчанию();
	
	Элементы.РазмерШрифта.МинимальноеЗначение = РаботаСHTMLВызовСервера.МинимальныйРазмерШрифта();
	Элементы.РазмерШрифта.МаксимальноеЗначение = РаботаСHTMLВызовСервера.МаксимальныйРазмерШрифта();
	Элементы.РазмерШрифта.Подсказка = СтрШаблон(
		НСтр("ru='Диапазон: %1 - %2'"),
		Формат(Элементы.РазмерШрифта.МинимальноеЗначение, Формат("ЧГ=")),
		Формат(Элементы.РазмерШрифта.МаксимальноеЗначение, Формат("ЧГ=")));
	
КонецПроцедуры

#КонецОбласти
