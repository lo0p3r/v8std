#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция СобратьHTML(ДеревоСтандартов) Экспорт
	
	ОсновнойДокумент = ПолучитьМакет("ОсновнойДокумент").ПолучитьТекст();
	ШаблонРазделаНавигации = ПолучитьМакет("РазделНавигации").ПолучитьТекст();
	ШаблонРаздела = ПолучитьМакет("Раздел").ПолучитьТекст();
	
	Корень = ДеревоСтандартов.Строки[0];
	Если Не Корень.Выбран Тогда
		Возврат ОсновнойДокумент;
	КонецЕсли;
	
	НомерРаздела = 1;
	ТелоДокумента = Новый Массив;
	Оглавление = Новый Массив;
	Шаблоны = Новый Структура("Раздел, РазделНавигации", ШаблонРаздела, ШаблонРазделаНавигации);
	
	СобратьРекурсивно(Корень, Шаблоны, Оглавление, ТелоДокумента, НомерРаздела);
	
	ОсновнойДокумент = СтрЗаменить(ОсновнойДокумент, "_NAVIGATION_", СтрСоединить(Оглавление, " "));
	ОсновнойДокумент = СтрЗаменить(ОсновнойДокумент, "_MAIN_BODY_",  СтрСоединить(ТелоДокумента, " "));
	
	Возврат ОсновнойДокумент;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СобратьРекурсивно(Дерево, Шаблоны, Оглавление, ТелоДокумента, НомерРаздела)
	
	Оглавление.Добавить("<ul>");
	
	Для Каждого СтрокаДерева Из Дерево.Строки Цикл
		
		Если Не СтрокаДерева.Выбран Тогда
			Продолжить;
		КонецЕсли;
		
		РазделНавигации =
			СтрШаблон(Шаблоны.РазделНавигации,
				Формат(НомерРаздела, "ЧГ="),
				СтрокаДерева.Наименование);
		Оглавление.Добавить(РазделНавигации);
		
		Если ЗначениеЗаполнено(СтрокаДерева.КодСтандарта) Тогда
			
			ТелоРаздела = СтрокаДерева.СодержаниеСтандартаHTML;
			ОчиститьСодержанияРаздела(ТелоРаздела);
			
			СодержаниеРаздела = СтрШаблон(Шаблоны.Раздел,
				Формат(НомерРаздела, "ЧГ="),
				СтрокаДерева.Наименование,
				ТелоРаздела,
				СтрокаДерева.СсылкаНаСтандарт);
			
			ТелоДокумента.Добавить(СодержаниеРаздела);
			
		КонецЕсли;
		
		НомерРаздела = НомерРаздела + 1;
		СобратьРекурсивно(СтрокаДерева, Шаблоны, Оглавление, ТелоДокумента, НомерРаздела);
		
	КонецЦикла;
	
	Оглавление.Добавить("</ul>");
	
КонецПроцедуры

Функция ОчиститьСодержанияРаздела(СодержаниеРаздела)
	
	НачалоТела = СтрНайти(СодержаниеРаздела, "<BODY>") + 6;
	КонецТела = СтрНайти(СодержаниеРаздела, "</BODY>");
	
	СодержаниеРаздела = Сред(СодержаниеРаздела, НачалоТела, КонецТела - НачалоТела);
	
	ПервыйDIV = СтрНайти(СодержаниеРаздела, "<DIV");
	Если ПервыйDIV = 0 Тогда
		ПервыйDIV = СтрНайти(СодержаниеРаздела, "<div");
	КонецЕсли;
	СодержаниеРаздела = Сред(СодержаниеРаздела, ПервыйDIV);
	
КонецФункции

#КонецОбласти

#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли