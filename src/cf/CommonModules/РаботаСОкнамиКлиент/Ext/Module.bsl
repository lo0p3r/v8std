#Область ПрограммныйИнтерфейс

// Получение начальной страницы приложения
// 
// Возвращаемое значение:
//  ОкноКлиентскогоПриложения, Неопределено - начальная страница
//
Функция НачальнаяСтраница() Экспорт
	
	ОткрытыеОкна = ПолучитьОкна();
	
	НачальнаяСтраница = Неопределено;
	Для Каждого КлиентскоеОкно Из ОткрытыеОкна Цикл
		
		Если КлиентскоеОкно.НачальнаяСтраница Тогда
			НачальнаяСтраница = КлиентскоеОкно;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат НачальнаяСтраница;
	
КонецФункции

// Получение основой формы просмотра стандартов
//
// Параметры:
//  Окно - ОкноКлиентскогоПриложения - окно, в котором ищется форма просмотра стандартов
// 
// Возвращаемое значение:
//  ФормаКлиентскогоПриложения
//
Функция ФормаПросмотраСтандартов(Окно) Экспорт
	
	ФормаСоСтандартами = Неопределено;
	
	Если Окно = Неопределено Тогда
		Возврат ФормаСоСтандартами;
	КонецЕсли;
	
	Для Каждого Форма Из Окно.Содержимое Цикл
		Если Форма.ИмяФормы = "Справочник.СтандартыРазработки.Форма.ФормаСпискаСПросмотром" Тогда
			ФормаСоСтандартами = Форма;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ФормаСоСтандартами;
	
КонецФункции

#КонецОбласти